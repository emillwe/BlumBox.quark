TITLE:: BlumBalance
summary:: Stereo balance transform
categories:: Libraries>Blumlein Box>Transforms, UGens>Multichannel>Panners
related:: Classes/BlumRotate

DESCRIPTION::
Balance a stereo signal towards the L or R axis

CLASSMETHODS::

METHOD:: ar, kr

ARGUMENT:: in
The stereo input, an array: [L, R]

ARGUMENT:: angle
Balance angle in radians between -pi/4, pi/4.

returns::
Balance transformed signal, an array: [L, R]

DISCUSSION::
Note that balance is the width transform applied in the LR domain.

EXAMPLES::

code::
(
~balanceTest = {
	var inL, inR;
	var env1, env2;
	var in;
	var lfo;
	var freqMod;

	freqMod = LFNoise2.kr(3.3).range(0.98, 1.02);

	// L and R channels
	inL = SinOsc.ar(440 * freqMod, mul: 0.3);
	inR = SinOsc.ar(330 * freqMod, mul: 0.3);

	// L and R envelopes
	#env1, env2 = EnvGen.kr(
		Env.perc(releaseTime: 0.3),
		Impulse.kr([10, 7])
	);

	// Stereo input
	in = [
		inL * env1,
		inR * env2
	];

	// Other inputs
	// in = SinOsc.ar([440, 330], mul: 0.3);

	// Transform LFO
	lfo = LFTri.kr(0.3).range(-pi/4, pi/4); // Note that values outside these bounds will wrap

	BlumBalance.ar(in, lfo);
}.play
)
::