TITLE:: BlumFollowAngle
summary:: Stereo angle analysis
categories:: Libraries>Blumlein Box>Analyses, UGens>Analysis
related:: Classes/BlumFollowRadius

DESCRIPTION::
Measure the encoding angle of a stereo source

CLASSMETHODS::

METHOD:: ar, kr

ARGUMENT:: in
The stereo input, an array: [L, R]

ARGUMENT:: size
Number of samples for Hilbert window (i.e. FFT size) or running sum. See link::Classes/RunningSum:: and link::Classes/HilbertW:: for more info.

ARGUMENT:: method
code::\instant:: for instantaneous analysis or code::\average:: for time-averaged analysis

returns::
Signal encoding angle

EXAMPLES::

code::
(
~angleTest = {
	var in, out;
	var lfo, rotLFO;
	var instant, average;
	var makeUpDelay;
	var size = 2048;
	var halfSize = (size / 2).asInteger;

	// Inputs
	// in = SinOsc.ar; // use with Pan2
	in = SinOsc.ar([440, 440]);
	// in = PinkNoise.ar(mul: [1.0, 1.0]);

	// LFOs
	lfo = SinOsc.kr(0.3); // angle oscillates between +/- pi/4
	rotLFO = LFSaw.kr(0.3).range(0, 2pi); // angle oscillates between +/- pi/2

	// apply lfo
	// in = Pan2.ar(in, lfo);
	in = BlumRotate.ar(in, rotLFO);

	// Instant and average output
	instant = BlumFollowAngle.ar(in, size: size, method: \instant);
	average = BlumFollowAngle.ar(in, size: size, method: \average);

	// match delay of instant and average for comparison
	makeUpDelay = ((halfSize * SampleDur.ir) - ControlDur.ir);
	average = DelayN.ar(average, makeUpDelay, makeUpDelay);

	// Compare instant vs average
	// out = (instant.neg + average);
	// out = average;
	out = instant;

	out.poll;
}.play
)
::