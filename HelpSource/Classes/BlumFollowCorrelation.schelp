TITLE:: BlumFollowCorrelation
summary:: Stereo correlation analysis
categories:: Libraries>Blumlein Box>Analyses, UGens>Analysis
related:: Classes/BlumFollowBalance

DESCRIPTION::
Analyze the degree of similarity between left and right channels of a stereo signal. Correlation is the distribution of power along the M-S axis.


CLASSMETHODS::

METHOD:: ar, kr

ARGUMENT:: in
The stereo input, an array: [L, R]

ARGUMENT:: size
Number of samples for Hilbert window (i.e. FFT size) or running sum. See link::Classes/RunningSum:: and link::Classes/HilbertW:: for more info.

ARGUMENT:: method
code::\instant:: for instantaneous analysis or code::\average:: for time-averaged analysis

returns::
Stereo correlation: a value on the interval [-1, 1]. A return value of -1 corresponds to L and R channels fully out of phase, while identical L and R channels will return 1. L and R signals in quadrature will return a correlation of 0.

table::
## emphasis::position:: || strong::middle:: || strong::de-correlated:: || strong::side::
## emphasis::value:: || code::1.0:: || code::0.0:: || code::-1.0::
::

EXAMPLES::

code::
// Run for 3s and plot
(
~correlationTest = {
	var in, out;
	var instant, average;
	var size = 2048;  // default
	var makeUpDelay;
	var halfSize = (size / 2).asInteger;

	// Inputs
	// in = SinOsc.ar([440, -440]); // out of phase: -1
	in = SinOsc.ar([440, 440]); // in phase: 1
	// in = SinOsc.ar(440, [0.5pi, 0.0]); // +quadrature: 0.0
	// in = SinOsc.ar(440, [0.5pi.neg, 0.0]); // -quadrature: 0.0
	// in = SinOsc.ar([440, LFSaw.kr(0.1).range(-440, 440)]); // oscillates between in and out of phase

	// Instant and average output
	instant = BlumFollowCorrelation.ar(in, size, \instant);
	average = BlumFollowCorrelation.ar(in, size, \average);

	// match delay of instant and average for comparison
	makeUpDelay = ((halfSize * SampleDur.ir) - ControlDur.ir);
	average = DelayN.ar(average, makeUpDelay, makeUpDelay);

	// Compare instant vs average
	// out = (instant.neg + average);
	// out = average;
	out = instant;

	out.poll
}.plot(3)
)
::